plugins {
    id 'maven-publish'
    id 'java'
    id 'eclipse'
    id "com.leonardo.rocha.AutoValueYamlPlugin" version '0.1.0'
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        credentials {
            username mavenUser
            password mavenPassword
        }
        url mavenRepo
    }
}

project.ext.set("generatedDir","${buildDir}/generatecode/src/main/java")

sourceCompatibility = 1.8

ext {
    autoValueVersion = '1.7'
}

project.description     = 'Repo that creates the Models for the mars-rover app'
project.group           = 'com.leonardo.rocha'
project.version         = '0.1.0'
project.ext.name        = 'mars-rover-photo-collector-models'

sourceSets {
    main {
        java {
            srcDirs generatedDir
			srcDirs "${buildDir}/generated/sources/annotationProcessor/java/main"
        }
    }
}

dependencies {
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.10.1'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.10.1'

    implementation "com.google.auto.value:auto-value-annotations:${autoValueVersion}"
    annotationProcessor "com.google.auto.value:auto-value:${autoValueVersion}"
}

eclipse {
	classpath {
		defaultOutputDir = file("build")
		file.whenMerged {
			entries.each { entry ->
				if(entry.kind == 'src' && entry.hasProperty('output')) {
					entry.output = entry.output.replace('bin/', "build/classes/java/")
				}
			}
		}
	}
}

publishing {
    publications {
        pluginPublication (MavenPublication) {
            from        components.java
            groupId     project.group
            artifactId  project.name
            version     project.version
        }
    }

    repositories {
        maven {
            url mavenRepo
            credentials {
                username mavenUser
                password mavenPassword
            }
        }
    }
}
